# Global dirs
data_gen_root_dpath := justfile_directory() / ".."
data_root_dpath := data_gen_root_dpath / ".."

# Input/source files
rules_markdown_source_path := data_gen_root_dpath / "Rules" / "Draw Steel Rules.md"
bestiary_markdown_source_path := data_gen_root_dpath / "Rules" / "Draw Steel Bestiary.md"
adventure_dir_markdown_source_path := data_gen_root_dpath / "Adventures"

# Staging dirs
staging_dpath := data_root_dpath / "staging"
staging_rules_dpath := staging_dpath / "rules"
staging_rules_linked_dpath := staging_rules_dpath / "md_sections_formatted_linked"
staging_bestiary_dpath := staging_dpath / "bestiary"

clean_and_prep:
    #!/usr/bin/env bash
    set -euo pipefail
    if [ -d "{{staging_dpath}}" ]; then
        rm -rf "{{staging_dpath}}"
    fi
    mkdir -p "{{staging_dpath}}"

gen: clean_and_prep
    #!/usr/bin/env bash
    set -euo pipefail

    # Rules
    just gen_rules
    just assemble_rules

gen_rules:
    #!/usr/bin/env bash
    set -euo pipefail

    # Convert OG markdown to html
    rules_html_fpath="{{staging_rules_dpath}}/html/Draw Steel Rules.html"
    just -f md_to_html/justfile run "{{rules_markdown_source_path}}" "$rules_html_fpath"

    # extract out sections using html xpath
    rules_html_sections_dpath="{{staging_rules_dpath}}/html_sections"
    just -f extract_html_sections/rules/justfile run "$rules_html_fpath" "$rules_html_sections_dpath"

    # Convert html sections to md sections
    rules_md_sections_dpath="{{staging_rules_dpath}}/md_sections"
    just -f html_sections_to_md/justfile run "$rules_html_sections_dpath" "$rules_md_sections_dpath"

    # Transform the MD section files to make them usable
    rules_md_sections_formatted_dpath="{{staging_rules_dpath}}/md_sections_formatted"
    just -f format_md/justfile run "$rules_md_sections_dpath" "$rules_md_sections_formatted_dpath"

    # Link MD section files to each other
    just -f link_md/justfile run "$rules_md_sections_formatted_dpath" "{{staging_rules_linked_dpath}}"

    # Format/Lint the linked markdown files
    just -f mdformat/justfile run "{{staging_rules_linked_dpath}}"
    # TODO - I should format the unlinked files?

assemble_rules:
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "--- Assembling Rules --- "
    cp -R "{{staging_rules_linked_dpath}}"/* "{{data_root_dpath}}/data-rules-md"
